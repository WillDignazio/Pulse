buildscript {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java' // Needed for protobuf
//    apply from: 'gradle/aot.gradle'
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'

    id "com.google.protobuf" version "0.6.1"
    id "com.commercehub.gradle.plugin.avro" version "0.3.4"
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// Avro contains a log4j instance for some ungodly reason
configurations.all {
    exclude module: 'slf4j-log4j12'
}

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:2.6.1'
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

configurations { // Needs to go before dependencies
    quasar
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.3'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.3'
    compile group: 'com.google.inject', name: 'guice', version: '4.0'
    compile 'com.google.inject.extensions:guice-assistedinject:4.0'
    compile group: 'commons-configuration', name: 'commons-configuration', version: '1.10'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'org.apache.solr', name: 'solr-solrj', version: '5.2.1'
    compile 'org.apache.httpcomponents:httpclient:4.5'
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'org.slf4j:slf4j-simple:1.6.1'
    compile 'com.google.protobuf:protobuf-java:2.6.1'
    compile 'org.apache.avro:avro-tools:1.7.7'
    compile 'org.hbase:asynchbase:1.6.0'
    compile 'org.json:json:20141113'
    compile 'org.apache.pdfbox:pdfbox:1.8.10'

    compile 'org.javafp:parsecj:0.2'
    compile 'org.apache.solr:solr-noggit:3.5.0'
    compile 'org.mapdb:mapdb:1.0.8'

    compile 'co.paralleluniverse:quasar-core:0.7.2:jdk8'
    compile 'co.paralleluniverse:quasar-actors:0.7.2'
    compile 'co.paralleluniverse:quasar-reactive-streams:0.7.2'

    quasar  "co.paralleluniverse:quasar-core:0.7.2:jdk8@jar"

    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.zookeeper:zookeeper:3.5.0-alpha'
    testCompile 'org.xerial:sqlite-jdbc:3.8.11'
}

// Enable javaagent for fibers on test code
tasks.withType(Test) {
    systemProperty 'co.paralleluniverse.fibers.verifyInstrumentation', 'true'
    systemProperty 'co.paralleluniverse.fibers.detectRunawayFibers', 'true'
    jvmArgs "-javaagent:${configurations.quasar.singleFile}"
}

classes {
    doFirst {
        println "Excuting scanSuspendables..."
        ant.taskdef(
                name: 'scanSuspendables', classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
                classpath: "build/classes/main:build/resources/main:${configurations.quasar.asPath}:${configurations.runtime.asPath}")
        ant.scanSuspendables(
                auto: false,
                suspendablesFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendables",
                supersFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendable-supers",
                append: true) {
            fileset(dir: sourceSets.main.output.classesDir)
        }
    }
}

defaultTasks 'build', 'writeClasspath'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        java {
            srcDir 'src/generated/main'
        }
        proto {
            // needed so that AsyncHBase's messages get built with 2.6.1
            srcDir 'build/extracted-include-protos/main'
        }
    }

    test {
        java {
            srcDir 'src/generated/main'
        }
    }
}

version = '1.0'
def jarTitle = 'Pulse Search Engine'
def mainClass = 'net.digitalbebop.AppBootstrapper'

jar {
    baseName = 'pulse'
    manifest {
        attributes 'Implementation-Title': jarTitle,
                   'Implementation-Version': version,
                   'Main-Class': mainClass
    }
}

task fatJar(type: Jar) {
    baseName = 'pulse-all'
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Implementation-Title': jarTitle,
                   'Implementation-Version': version,
                   'Main-Class': mainClass
    }
    with jar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

/*
 * Write out the classpath for all internal dependencies, then append the
 * actual expected build JAR to the end of it.
 */
task writeClasspath(dependsOn: build) << {
    new File(buildDir.absolutePath + "/runtime.classpath").withWriter {
        out -> out.writeLine(sourceSets.main.runtimeClasspath.asPath +
                             ":" + jar.archivePath);
    }
    new File(buildDir.absolutePath + "/quasar-core.path").withWriter {
        out -> out.writeLine(configurations.quasar.asPath);
    }
}

task suspendDetection() {
    ant.taskdef(name:'scanSuspendables', classname:'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
            classpath: "build/classes/main:build/resources/main:${configurations.runtime.asPath}")
    ant.scanSuspendables(
            auto:true,
            suspendablesFile: "src/main/resources/META-INF/suspendables",
            supersFile: "src/main/resources/META-INF/suspendable-supers") {
        fileset(dir: sourceSets.main.output.classesDir)
    }
}

test {
    // enable TestNG support (default is JUnit)
    useJUnit()

    testSrcDirs = ['src/test/java']

    // Toggle standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = false

    // set heap size for the test JVM(s)
    minHeapSize = "128m"
    maxHeapSize = "512m"

    // set JVM arguments for the test JVM(s)
    jvmArgs '-XX:MaxPermSize=512m'

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}
